#!/usr/bin/perl -w

use strict;
use IO::Uncompress::Gunzip;
use File::Temp qw{tempdir tempfile};

if ( scalar @ARGV != 4 ) {
  print STDERR "Usage: $0 FILENAME OUTPUT DIRECTION MILLISECONDS_TO_SKIP\n";
  exit;
}

my ( $filename, $svg_name, $desired_direction, $start_grading_time ) = @ARGV;

my $gz = new IO::Uncompress::Gunzip $filename or die;

my $MS_PER_SLICE = 500;
my $last_ms = 0;
my $current_slice = 0;
my $capacity_sum = 0;
my $used_sum = 0;

my $output = "";

while ( <$gz> ) {
  chomp;

  my ( $direction, $ms, $event, $field1, $field2 ) = split /\s+/, $_;
  next unless $direction eq $desired_direction;
  die "Bad event $event" unless $event eq "underflow" or $event eq "delivery";

  next if $ms <= $start_grading_time;

  $ms -= $start_grading_time;

  if ( $event eq "delivery" ) {
    my $bits = $field2 * 8;
    $used_sum += $bits;
    $capacity_sum += $bits;
  } elsif ( $event eq "underflow" ) {
    my $bits = $field1 * 8;
    $capacity_sum += $bits;
  }

  die "nonmonotonic timestamps" unless $ms >= $last_ms;
  $last_ms = $ms;

  my $new_slice = int( $ms / $MS_PER_SLICE );

  while ( $new_slice != $current_slice ) {
    # output new slice
    my $slice_middle = int( ($current_slice + 0.0) * $MS_PER_SLICE );
    $slice_middle *= 1.e-3;
    my $capacity_mbps = $capacity_sum * (1.e3 / $MS_PER_SLICE) * 1.e-6;
    my $used_mbps = $used_sum * (1.e3 / $MS_PER_SLICE) * 1.e-6;
    $output .= "$slice_middle $capacity_mbps $used_mbps\n";
    $capacity_sum = $used_sum = 0;
    $current_slice++;
  }
}

my $temp_filename = rand() . '-' . $$ . '-' . $svg_name;

open GNUPLOT, '| gnuplot' or die;
print GNUPLOT <<END;
set title "Throughput"
set xlabel "time (s)"
set ylabel "Mbps"
set ytics format "% 6.0f"
set style fill solid 0.4 border
set terminal svg size 1024,560 fixed  fname 'Arial'  fsize 12 rounded solid
set output "$temp_filename"
plot "-" using 1:2 title "Capacity" with filledcurves above x1 lw 1, "-" using 1:3 with lines lt 3 lw 4 title "Protocol"
END

print GNUPLOT $output;
print GNUPLOT "\ne\n";
print GNUPLOT $output;

close $gz or die "$!";
close GNUPLOT or die "$!";

rename( $temp_filename, $svg_name ) or die "$!";

